# Title

Exceptions management

# Status

Accepted

# Context

The kernel needs a way to handle exceptions. There are 2 types of exceptions: **synchronous** which are asserted by the program or the core itself and **asynchronous** which are asserted by **interrupts** externals to the core. 

A high-level mechanism is used which will dispatch exceptions to the dedicated handler : **interrupt_handler** or **exception_handler**.

In linux, this is implemented in the [handle_exception](https://elixir.bootlin.com/linux/latest/source/arch/riscv/kernel/entry.S#L85) routine. On **riscv**, this function is registered in **mtvec** and is called whenever an exception is asserted. Some interesting parts of the implementation can be seen here after:

```asm
SYM_CODE(handle_exception)
    ...
    csrr s4, CSR_CAUSE
	...
	la ra, ret_from_exception
	...
	bge s4, zero, 1f

	/* Handle interrupts */
	tail do_irq
1:
	/* Handle other exceptions */
	slli t0, s4, RISCV_LGPTR
	la t1, excp_vect_table
	la t2, excp_vect_table_end
	add t0, t1, t0
	/* Check if exception code lies within bounds */
	bgeu t0, t2, 1f
	REG_L t0, 0(t0)
	jr t0
1:
	tail do_trap_unknown
SYM_CODE_END(handle_exception)
```

When observing the code, it can be noticed that the register **mcause** saved in **s4** is used to dispatch interrupts and exceptions: **bge s4, zero, 1f** instruction jumps to **do_irq** if an interrupt triggered the handler. Otherwise, a dedicated **exception handler** is called. These handler addresses are registered in an array called **excp_vect_table**:

```asm
SYM_CODE_START(excp_vect_table)
	RISCV_PTR do_trap_insn_misaligned
	ALT_INSN_FAULT(RISCV_PTR do_trap_insn_fault)
	RISCV_PTR do_trap_insn_illegal
	RISCV_PTR do_trap_break
	RISCV_PTR do_trap_load_misaligned
	RISCV_PTR do_trap_load_fault
	RISCV_PTR do_trap_store_misaligned
	RISCV_PTR do_trap_store_fault
	RISCV_PTR do_trap_ecall_u /* system call */
	RISCV_PTR do_trap_ecall_s
	RISCV_PTR do_trap_unknown
	RISCV_PTR do_trap_ecall_m
	/* instruciton page fault */
	ALT_PAGE_FAULT(RISCV_PTR do_page_fault)
	RISCV_PTR do_page_fault   /* load page fault */
	RISCV_PTR do_trap_unknown
	RISCV_PTR do_page_fault   /* store page fault */
excp_vect_table_end:
SYM_CODE_END(excp_vect_table)
```

The following code computes the offset in the array with the **mcause** register value, loads the handler address in **t0** and jumps to it:

```asm
    slli t0, s4, RISCV_LGPTR
	la t1, excp_vect_table
	la t2, excp_vect_table_end
	add t0, t1, t0
	/* Check if exception code lies within bounds */
	bgeu t0, t2, 1f
	REG_L t0, 0(t0)
    jr t0
```

It also checks if the exception has a corresponding handler saved in the array through **bgeu t0, t2, 1f**. If not, it jumps to **do_trap_unknown**. The **slli t0, s4, RISCV_LGPTR** shifts **mcause** value to the left, this converts the value to a usable address offset.

Eventually, if **mcause** value is **11** (which is equivalent to an **ecall**) for example, the **handle_unknown_exception** function will jump to **do_trap_ecall_m**.

One last thing to notice is the **ret_from_exception** routine. This function is called when an exception handler returns. It's loaded in **ra** register before the dispatch. To prevent the core to overwrite it when calling sub-routines, the **tail** instruction is used: the behaviour is similar to the **call** instruction but it doesn't update the **ra** value.

A pretty similar flow can be seen in **lk**, the [riscv_exception_entry](https://github.com/littlekernel/lk/blob/master/arch/riscv/asm.S#L139) calls [riscv_exception_handler](https://github.com/littlekernel/lk/blob/master/arch/riscv/exceptions.c#L110C21-L110C21) which will dispatch the event to the dedicated handler. A part of the routine can be seen here after: 

```C
void riscv_exception_handler(long cause, ulong epc, struct riscv_short_iframe *frame, bool kernel) {
    ...
    // top bit of the cause register determines if it's an interrupt or not
    if (cause < 0) {
        switch (cause & LONG_MAX) {
            case RISCV_INTERRUPT_XSWI: // machine software interrupt
                ret = riscv_software_exception();
                break;
            case RISCV_INTERRUPT_XTIM: // machine timer interrupt
                ret = riscv_timer_exception();
                break;
            case RISCV_INTERRUPT_XEXT: // machine external interrupt
                ret = riscv_platform_irq();
                break;
            default:
                fatal_exception(cause, epc, frame, kernel);
        }
    } else {
        // all synchronous traps go here
        switch (cause) {
            case RISCV_EXCEPTION_ENV_CALL_U_MODE: // ecall from user mode
                riscv_syscall_handler(frame);
                break;
            default:
                fatal_exception(cause, epc, frame, kernel);
        }
    }
    ...
}
```
This routine is written in C when all the work is done in assembly in linux. One function is defined by interrupt or exception type. This is very similar to the **zircon** design (as zircon is a fork of lk) but more handlers are defined in [riscv64_exception_handler](https://cs.opensource.google/fuchsia/fuchsia/+/main:zircon/kernel/arch/riscv64/exceptions_c.cc;l=259) to manage exceptions. This handler is also called by [riscv64_exception_entry](https://cs.opensource.google/fuchsia/fuchsia/+/main:zircon/kernel/arch/riscv64/exceptions.S;l=231) as in **lk**.

One big difference between **lk/zircon** and **linux** is that **linux always saves context whenever the exception comes from the kernel or the user mode**:

```asm
SYM_CODE_START(handle_exception)
	...
_save_context:
	REG_S sp, TASK_TI_USER_SP(tp)
	REG_L sp, TASK_TI_KERNEL_SP(tp)
	addi sp, sp, -(PT_SIZE_ON_STACK)
	REG_S x1,  PT_RA(sp)
	REG_S x3,  PT_GP(sp)
	REG_S x5,  PT_T0(sp)
	save_from_x6_to_x31
	...
SYM_CODE_END(handle_exception)

SYM_CODE_START_NOALIGN(ret_from_exception)
	...
	REG_L x1,  PT_RA(sp)
	REG_L x3,  PT_GP(sp)
	REG_L x4,  PT_TP(sp)
	REG_L x5,  PT_T0(sp)
	restore_from_x6_to_x31

	REG_L x2,  PT_SP(sp)
    ...
SYM_CODE_END(ret_from_exception)
```

**lk/zircon only saves context if the exception comes from the user mode**:
```asm
FUNCTION(riscv_exception_entry)
    // check to see if we came from user space
    csrrw   sp, RISCV_CSR_XSCRATCH, sp
    bnez    sp, 1f

    // put the stack back
    csrrw   sp, RISCV_CSR_XSCRATCH, sp
    j       kernel_exception_entry

1:
    // came from user space
    j       user_exception_entry
END_FUNCTION(riscv_exception_entry)

LOCAL_FUNCTION(kernel_exception_entry)
    // we came from kernel space so tp and gp are okay
    save_regs 0

    // bool kernel = true
    li     a3, 1
    jal    riscv_exception_handler

    restore_regs 0

    RISCV_XRET
END_FUNCTION(kernel_exception_entry)

LOCAL_FUNCTION(user_exception_entry)
    // we came from user space, assume gp and tp have been trashed
    save_regs 1

    // bool kernel = false
    li     a3, 0
    jal    riscv_exception_handler

    restore_regs 1

    RISCV_XRET
END_FUNCTION(user_exception_entry)
```

**save_regs / restore_regs** only save registers in the **user_exception_entry** routine.

# Decision

The final implementation is heavily inspired by the linux one. **_trap_handler** dispatches the exception to the right handler. This routine is very similar to **handle_exception** described in the previous section.

The design of the **_exception_vector_table** is very speficic as it allows the **mcause** value to be used to compute the offset in the table. Indeed, **each handler is placed at a multiple of 0x10**. This allows to compute each address in the array as follows: **mcause * 0x10 + table_base_address**. This is what this code does:

```asm
    slli    t0, t0, RISCV_PTR_LENGTH
    la      t1, _exception_vector_table
    ...
    add     t0, t1, t0
```

Eventually, when an exception occurs, it passes through the following path:

```C
                    Task_A

            ┌─────────────────────┐
            │  previous_function  │
            └──────────┬──────────┘
                       │
                ┌──    │
                │      │
                │ ┌────▼────┐
                │ │  Ecall  │
                │ └────┬────┘
                │      │
                │      │      ┌───────────────┐    ┌─────────────────────────────┐
                │      └──────► _trap_handler │    │                             │
                │             └───────┬───────┘    │ _exception_vector_table     │
                │                     │            │                             │
                │                     │            │                             │
                │                     │            │  handle_page_fault          │
                │                     │            │                             │
  task_yield()  │                     │            │  handle_load_access_fault   │
                │                     │            │                             │
                │                     │            │  ...                        │
                │                     │            │                             │
                │                     └────────────►  handle_syscall             │
                │                                  │                             │
                │                                  │  ...   │                    │
                │            ┌────────────────┐    │        │                    │
                │            │ _ret_from_trap ◄────┼────────┘                    │
                │            └────────┬───────┘    │                             │
                │  ┌─────┐            │            └─────────────────────────────┘
                │  │ ret ◄────────────┘
                │  └──┬──┘
                │     │
                └──   │
                      │
              ┌───────▼─────────┐
              │  next_function  │
              └─────────────────┘
```
In this example, the **ecall** instruction triggers the **_trap_handler**. 

# Consequences

The implementation allows **portability** as all exceptions trigger **_trap_handler** which will dispatch the event to the right handler. A more optimized solution might use the **hardware dispatch mechanism** available in **riscv architecture** (see the **vectored mode** described in RISCV Privileged ISA, 3.1.7).